plugins {
    id 'eclipse'
    id 'idea'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

description = "${project.name} - a Java wrapper for the MediaWiki API"
group = "org.fastily"
version = "2.0.0"

repositories {
    mavenCentral()
}


dependencies {
    api "com.google.code.gson:gson:2.8.6"
    api "com.squareup.okhttp3:okhttp:4.9.0"
    api "org.slf4j:slf4j-api:1.7.30"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    //testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.5.2'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
}


test {
    useJUnitPlatform()
}


java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}


javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/en/java/javase/11/docs/api/"
        //links "https://square.github.io/okio/1.x/okio/"
        //links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"

        // links "http://square.github.io/okhttp/3.x/mockwebserver/"
        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}

wrapper {
    gradleVersion = '6.8'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'jwiki'
                description = 'A library for effortlessly interacting with Wikipedia/MediaWiki'
                inceptionYear = '2014'
                url = 'https://github.com/fastily/jwiki'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'fastily'
                        name = 'Fastily'
                        email = 'fastily@users.noreply.github.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/fastily/jwiki.git'
                    developerConnection = 'scm:git:ssh://github.com/fastily/jwiki.git'
                    url = 'https://github.com/fastily/jwiki'
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username hasProperty("ossrhUsername") ? "$ossrhUsername" : System.getenv('ossrhUsername')
                password hasProperty("ossrhPassword") ? "$ossrhPassword" : System.getenv('ossrhPassword')
            }
        }
    }
}


//signing {
//    sign publishing.publications.mavenJava
//}